#Options
GenerateScanner = true
GenerateParser = true
Language = Java
ScannerName = Lexico
ParserName = Sintatico
SemanticName = Semantico
ScannerCaseSensitive = true
ScannerTable = Compact
Input = Stream
Parser = LL
#RegularDefinitions
digito: [0-9]
min: [a-z]
mai: [A-Z]
letra: {mai} | {min}
int_aux: [1-9] ({digito}? {digito}?)
inteiro: {int_aux}(\.[0-9] [0-9] [0-9])*|0





 
#Tokens
//A) 
 identificador : ({mai} ({min}{mai}?)* | ({min}{mai}?)+) ({digito}?{digito})?

//B)
constante_int: {inteiro}

//C)
constante_float: {inteiro}, {digito}+

//D)
binario: 0(b|B)(0|1)+

//E
string: \"[^\n\"\\]*\"

//F
:! #[^\n]*

//G
:! (\{\n) [^\{\}]* (\n\}) | (\{\n\})


//constante_int - faz de todas 

//palavras reservadas 
check = identificador: "check" //como a palavra tem que ser escrita
and = identificador: "and"
else = identificador: "else"
end = identificador: "end"
false = identificador: "false"
if = identificador: "if"
not = identificador: "not"
or = identificador: "or"
read = identificador: "read"
true = identificador: "true"
write = identificador: "write"
writeln = identificador: "writeln"

// simbolos especiais 

","
":"
"="
";"
"("
")"
"=="
"!="
"<"
"<="
">"
">="
"+"
"-"
"*"
"/"


//formata��o   de formata��o - reconhecer e ignorar
:[\s\t\n] // ignorar

// comentarios 
:! # [^\n]*


#NonTerminals
<programa>
<lista_de_comandos>
<lista_de_comandos_1> 
<comando>
<comando_atribuicao>
<comando_atribuicao_1> 	
<comando_entrada_dados>
<comando_saida_dados>
<comando_selecao> 
<comando_repeticao>
<comando_repeticao_1>
<lista_expressoes>
<lista_expressoes_1>
<lista_identificadores>
<lista_identificadores_1>
<expressao>
<expressao_>	
<elemento>
<relacional>
<relacional_>	
<operador_relacional>
<aritmetica>	
<aritmetica_>
<termo>		
<termo_>
<fator>
<else>
#Grammar
<programa>		::= #15 #21 <lista_de_comandos> #16;

<lista_identificadores> 	::= identificador #22 <lista_identificadores_1> ;
<lista_identificadores_1> 	::= î | "," <lista_identificadores> ;

<lista_de_comandos> 	::= <comando> ";" <lista_de_comandos_1> ;
<lista_de_comandos_1> 	::= î |  <lista_de_comandos>;
<comando> 		::= <comando_atribuicao> | <comando_entrada_dados> | <comando_saida_dados> |<comando_selecao> | <comando_repeticao> ;

<comando_atribuicao> 	::= <lista_identificadores> "=" <expressao> <comando_atribuicao_1> ;
<comando_atribuicao_1> 	::= #24 î | #25 ":" if <expressao> #26;

<comando_entrada_dados> 	::= read "(" <lista_identificadores> #27 ")" ;

<comando_saida_dados> 	::= write "(" <lista_expressoes> ")" | writeln "(" <lista_expressoes> ")" #17;
<lista_expressoes> 	::= <expressao> #14 <lista_expressoes_1> ;
<lista_expressoes_1>	::= î | "," <lista_expressoes> ;
 
<comando_selecao>       	::= if "(" <expressao> ")" #28 <lista_de_comandos> <else> end #29;
<else>     		::= î | #30 else <lista_de_comandos>;

<comando_repeticao> 	::= check #31 "(" <expressao> ")" ":" if <comando_repeticao_1> #32 <lista_de_comandos> end #33;
<comando_repeticao_1>	::= true | false;


<expressao>			::= <elemento> <expressao_>  ;
<expressao_>			::= î | and <elemento> #18 <expressao_> | or <elemento> #19 <expressao_> ;
<elemento>			::= <relacional> | true #11 | false #12 | not <elemento> #13;

<relacional>			::= <aritmetica> <relacional_> ;
<relacional_>			::= î | <operador_relacional> #9 <aritmetica> #10 ;
<operador_relacional>		::= "==" | "!=" | "<" | "<=" | ">" | ">=" ;

<aritmetica>			::= <termo> <aritmetica_>  ;
<aritmetica_>			::= î | "+" <termo> #1 <aritmetica_> | "-" <termo> #2 <aritmetica_> ;

<termo>				::= <fator> <termo_> ;
<termo_>			::= î | "*" <fator> #3 <termo_> | "/" <fator> #4 <termo_> ;

<fator>			::= identificador   #23 |
			    constante_int   #5  | 
		   	    constante_float #6  | 
			    binario             |
			    string          #20 | 
		   	    "(" <expressao> ")" |
	    	  	    "+" <fator>         | 
	    		    "-" <fator> 	  #8;

